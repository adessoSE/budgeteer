import org.gradle.internal.jvm.Jvm

task wrapper(type: Wrapper) {
    gradleVersion '3.4.1'
}

// alle Skripte aus ./gradle.d in sortierter Reihenfolge einbinden
file('gradle.d').listFiles().sort().each {
    if (it =~ /.*\.gradle$/) {
        apply from: relativePath(it)
    }
}

allprojects {
    version = getBuildVersion('1.0.9.BETA')
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    repositories {
        jcenter()
        maven {
            name "Apache Snapshots"
            url "https://repository.apache.org/content/repositories/snapshots/"
        }
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: junit_version
        testCompile group: 'org.springframework', name: 'spring-test', version: spring_version
        testCompile group: 'org.mockito', name: 'mockito-core', version: mockito_version
    }

    compileJava {
        sourceCompatibility = '1.7'
        targetCompatibility = '1.7'
        compileJava.options.bootClasspath = "${Jvm.current().javaHome}/jre/lib/rt.jar"
    }

    jacocoTestReport {
        reports {
            xml.enabled false
            csv.enabled false
            html.destination "${buildDir}/jacocoHtml"
        }
    }
}
