import org.apache.tools.ant.filters.ReplaceTokens

version = '1.0.7.BETA'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: "${spring_boot_version}"
        classpath group: 'org.springframework', name: 'springloaded', version: "${springloaded_version}"
    }
}

apply plugin: 'war'
apply plugin: 'spring-boot'

configurations {
    querydslapt

    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    all*.exclude group: 'log4j', module: 'log4j'
    all*.exclude group: 'org.slf4j', module: 'log4j-over-slf4j'
    all*.exclude group: 'org.apache.wicket', module: 'wicket'
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        /* alle .java Dateien landen im war */
        resources.srcDirs = ['src/main/java', 'src/main/resources']
    }
}

processResources {
    filter ReplaceTokens, tokens: [
            "application.version": project.property("version")
    ]
}

dependencies {
    compile project(':budgeteer-aproda-importer')
    compile project(':budgeteer-ubw-importer')
    compile project(':budgeteer-resourceplan-importer')

    compile(
            [group: 'org.apache.wicket', name: 'wicket-spring', version: "${wicket_spring_version}"],
            [group: 'org.wicketstuff', name: 'wicketstuff-lazymodel', version: "${wicketstuff_lazymodel_version}"],
            [group: 'com.googlecode.wicked-charts', name: 'wicked-charts-wicket7', version: "${wicketcharts_version}"],
    )

    compile group: 'org.springframework', name: 'spring-webmvc', version: "${spring_webmvc_version}"
    compile(
            [group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: "${spring_boot_version}"],
            [group: 'org.springframework.boot', name: 'spring-boot-starter-logging', version: "${spring_boot_version}"],
            [group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: "${spring_boot_version}"],
    )
    providedCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-jetty', version: "${spring_boot_version}")

    compile group: 'org.slf4j', name: 'slf4j-api', version: "${slf4j_version}"
    compile group: 'ch.qos.logback', name: 'logback-classic', version: "${logback_version}"
    compile group: 'ch.qos.logback', name: 'logback-core', version: "${logback_version}"

    compile group: 'org.reflections', name: 'reflections', version: "${reflections_version}"
    compile group: 'org.apache.commons', name: 'commons-lang3', version: "${commons_lang_version}"
    compile group: 'commons-codec', name: 'commons-codec', version: "${commons_codec_version}"

    compile group: 'joda-time', name: 'joda-time', version: "${joda_time_version}"
    providedCompile group: 'org.joda', name: 'joda-convert', version: "${joda_convert_version}"
    compile group: 'org.joda', name: 'joda-money', version: "${joda_money_version}"
    compile group: 'de.jollyday', name: 'jollyday', version: "${jollyday_version}"

    compile(
            [group: 'com.mysema.querydsl', name: 'querydsl-apt', version: "${query_dsl_version}"],
            [group: 'com.mysema.querydsl', name: 'querydsl-jpa', version: "${query_dsl_version}"]
    )

//    compile group: 'org.liquibase',  name: 'liquibase-core', version: '3.3.2'

    runtime(
            [group: 'org.hsqldb', name: 'hsqldb', version: "${hsqldb_version}"],
            [group: 'mysql', name: 'mysql-connector-java', version: "${mysql_version}"],
            files("lib/ojdbc7.jar", "lib/orai18n.jar")
    )

    testCompile(
            [group: 'org.kubek2k', name: 'springockito', version: "${springockito_version}"],
            [group: 'org.kubek2k', name: 'springockito-annotations', version: "${springockito_version}"],
            [group: 'com.github.springtestdbunit', name: 'spring-test-dbunit', version: "${spring_dbunit_version}"],
            [group: 'org.dbunit', name: 'dbunit', version: "${dbunit_version}"],
    )

    testCompile "org.springframework.boot:spring-boot-starter-test" // version?

    providedCompile group: 'org.projectlombok', name: 'lombok', version: "${lombok_version}"
}

sourceSets {
    generated {
        java {
            srcDirs = ['src/generated/java']
        }
    }
}

task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {

    source = sourceSets.main.java
    classpath = configurations.compile + configurations.querydslapt
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.mysema.query.apt.jpa.JPAAnnotationProcessor"
    ]

    destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

compileJava {
    dependsOn generateQueryDSL
    source generateQueryDSL.destinationDir
}

compileGeneratedJava {
    dependsOn generateQueryDSL
    options.warnings = false
    classpath += sourceSets.main.runtimeClasspath
}

clean {
    delete sourceSets.generated.java.srcDirs
}

bootRun {
    // default application configuration for running application via bootRun in development mode
    // (in production, these properties are defined in application.properties)
    jvmArgs '-Xdebug',
            '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=9009',
            '-Dserver.port=8080',
            '-Dwicket.configurationType=DEVELOPMENT',
            //'-Dspring.datasource.url=jdbc:hsqldb:file:../../database/budgeteer;shutdown=true;hsqldb.write_delay=false;',
            '-Dspring.datasource.url=jdbc:hsqldb:mem:budgeteer',
            '-Dspring.datasource.driverClassName=org.hsqldb.jdbcDriver',
            '-Dspring.datasource.username=sa',
            '-Dspring.datasource.password=',
            '-Dspring.jpa.hibernate.ddl-auto=update'
}

idea {
    module {
        scopes.PROVIDED.plus += [configurations.providedCompile]
        sourceDirs += sourceSets.generated.java.srcDirs
        downloadSources = true
    }
    // cf. https://discuss.gradle.org/t/the-idea-plugin-breaks-the-new-intellij-13-iml-configuration/2456/10
    module.iml.withXml {
        def xmlParser = new XmlParser()
        def content = it.asNode().component.content[0]
        def atts = content.find { it.@url == 'file://$MODULE_DIR$/src/main/resources' }.attributes()
        atts.remove('isTestSource')
        atts.put('type', 'java-resource')
        atts = content.find { it.@url == 'file://$MODULE_DIR$/src/test/resources' }.attributes()
        atts.remove('isTestSource')
        atts.put('type', 'java-test-resource')
        atts = content.find { it.@url == 'file://$MODULE_DIR$/src/generated/java' }?.attributes()
        if (atts)
            atts.put('generated', 'true')
        else {
            def generatedNode = '''
                <sourceFolder url="file://$MODULE_DIR$/src/generated/java" isTestSource="false" generated="true" />'''
            content.append(xmlParser.parseText(generatedNode))
        }
        def springFacet = '''
            <component name="FacetManager">
                <facet type="Spring" name="Spring">
                  <configuration>
                    <fileset id="fileset" name="Spring Application Context" removed="false">
                      <file>file://$MODULE_DIR$/src/main/java/org/wickedsource/budgeteer/boot/WebInitializer.java</file>
                    </fileset>
                  </configuration>
                </facet>
                <facet type="web" name="Web">
                  <configuration>
                    <webroots>
                      <root url="file://$MODULE_DIR$/src/main/webapp" relative="/" />
                    </webroots>
                    <sourceRoots>
                      <root url="file://$MODULE_DIR$/src/generated/java" />
                      <root url="file://$MODULE_DIR$/src/main/java" />
                      <root url="file://$MODULE_DIR$/src/main/resources" />
                    </sourceRoots>
                  </configuration>
                </facet>
            </component>'''
        it.asNode().append(xmlParser.parseText(springFacet))
    }
}
